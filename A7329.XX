	.NLIST
	.MACRO PAIR A,B
	.BYTE	^D<A>*20+^D<B>
	.ENDM
	.LIST
	.TITLE POOLSHARK 6/14/76 TAH
	.ASECT
	.=70000
IRQRST	=64000
DISP	=2000
HPOS	=4000
VPOS	=6000
P1	=10000
P2	=10001
CN1	=10002
CN2	=10003
SCRAT	=12000
SCORES	=14000
CLICK	=16000
CONTRL	=60000
BUMP	=62000
STF1	=66002
STN1	=66003
STF2	=66004
STN2	=66005
STF12	=66006
STN12	=66007
OBJH	=0			;HPOS FOR DISPLAY
OBJV	=20			;VPOS
OBJHL	=40			;HPOS LOW
OBJVL	=60			;VPOS LOW
OBJHV	=100			;HV
OBJHVL	=120			;HV LOW
OBJVV	=140			;VV
OBJVVL	=160			;VL LOW
J	=200			;WORK
K	=201			;WORK
L	=202			;WORK
M	=203			;WORK
X2	=204			;HOLD X DURING INTERRUPT
X3	=206			;HOLD X
TPTR	=210			;BUILD TABLE PTRS
SPT	=212			;SAVE STACK
MORA	=214			;SOURCE FOR EXTRAS
MORD	=216			;DEST. FOR EXTRAS
TSTH	=220			;PARAMETERS OF INCIDENT BALL
TSTHL	=221
TSTV	=222
TSTVL	=223
TSTHV	=224
TSTHVL	=225
TSTVV	=226
TSTVVL	=227
SYNC	=230
TIME	=231
RTC	=231
THETA	=232
PSYNC	=234
R0	=240
R1	=241
R2	=242
R3	=243
R4	=244
R5	=245
R6	=246
R7	=247
BP	=250
SCR1	=251
SCR2	=253
TIMER	=255
HEAT	=257
PFG	=260
CF	=261
X4	=262
TEMP	=264
R8	=266
R9	=267
ZCNT	=270
HOLDHV	=271
HOLDVV	=272
RACK	=276
PLYNUM	=275
CREDIT	=274
AT	=273
COIN	=377
CNTACT	=300				;300-317
MODE = 277
GOTYM=320
GOFLAG=321
ATTYM=322
PNUM=323
QTV=324
QTH=325
QTM=326
BONUS	=330
EXPLY	=10001
DIAG	=40000


BEGIN:	.BYTE	36,0		;CHECKSUM & COMPLEMENT
TESTPS:	STAA	E,STN12			;START LIGHTS ON
	JSR	E,BLANK			;CLEAR SCREEN
	JSR	E,SWITCH		;TEST SWITCHES
	LDAA	I,13			;"TEST"
	JSR	E,MESOUT
	STAA	E,IRQRST
	SEI
	LDX	I,COIN
	LDAA	I,COIN
TLP0:	STAA	X,0
	DEX
	DECA
	BNE	TLP0
	STAA	X,0
	LDX	I,COIN
	LDAA	I,COIN
TLP1:	CMPA	X,0
	BNE	RMER
	CLR	X,0
	DEX
	DECA
	BNE	TLP1
	CMPA	X,0
	BEQ	CKSM
RMER:	TSTA
	BMI	RM2
	LDAA	I,14
RMER1:	JSR	E,MESOUT
	JSR	E,TSWIT
	BRA	S,-3
RM2:	LDAA	I,15
	BRA	RMER1
CKSM:	CLI
	LDX	I,BEGIN
	LDAA	X,0
	INX
	INX
ROMA:	SUBA	X,0
	INX
	CPX	I,BEGIN+2000
	BNE	ROMA
	STAA	E,CN2
	TSTA
	BEQ	ROMB
	LDAA	I,16			;C
	JSR	E,MESOUT
	JSR	E,TSWIT
	BRA	S,-3
ROMB:	LDAA	E,BEGIN+3776
ROMB1:	SUBA	X,0
	INX
	CPX	I,BEGIN+3776
	BNE	ROMB1
	STAA	E,CN2
	TSTA
	BEQ	ROMC
	LDAA	I,17			;D
	JSR	E,MESOUT
ROMC:	INX
	INX
	LDAA	X,0
	INX
	INX
ROMC1:	SUBA	X,0
	INX
	CPX	I,BEGIN+6000
	BNE	ROMC1
	STAA	E,CN2
	TSTA
	BEQ	TSTOK
	LDAA	I,20			;E
	JSR	E,MESOUT
TSTOK:	LDAA	X,0
	INX
	INX
ROMD:	SUBA	X,0
	CPX	I,BEGIN+7777
	BEQ	TSTOKA
	INX
	BRA	ROMD
	STAA	E,CN2
	TSTA
	BEQ	TSTOKA
	LDAA	I,37			;F
	JSR	E,MESOUT
	JSR	E,TSWIT
	BRA	S,-3
TSTOKA:	LDAA	I,12			;OK
	JSR	E,MESOUT
TSTLPE:	LDAA	I,26			;POT ADJUST
	JSR	E,MESOUT
	LDAA	I,27			;P1X
	JSR	E,MESOUT
	CLR	E,R0
	LDX	I,P1
	JSR	E,POTADJ
	COM	E,R0
	LDAA	I,30			;P1Y
	JSR	E,MESOUT
	JSR	E,PXAL
	CLR	E,R0
	LDAA	I,31			;P2X
	JSR	E,MESOUT
	LDX	I,P2
	JSR	E,POTADJ
	COM	E,R0
	LDAA	I,32			;P2Y
	JSR	E,MESOUT
	JSR	E,PXAL
	JSR	E,BLANK
	LDAA	I,40			;TEST COMPLETE
	JSR	E,MESOUT
TSTLPZ:	JSR	E,SWITCH
	JSR	E,TSWIT
	BRA	TSTLPZ
POTADJ:	STX	D,R1			;R0=0 => X POT
PXAL:	JSR E,SWITCH
	LDX	D,R1			;R0=200 => Y POT
	JSR	E,TSWIT
	JSR	E,GETVAL
	TST	E,R0
	BPL	XPOT
	CMPB	I,10
	BRA	S,+4
XPOT:	CMPA	I,10
	BEQ	PXDN
	BCS	PXUP
	STAA	E,BUMP+1
PXUN:	LDAA	I,36			;NOT ALIGNED
	JSR	E,MESOUT
	LDAA	I,34
	JSR	E,MESOUT		;CLEAR "PUSH START2"
	BRA	PXAL
PXUP:	STAA	E,BUMP
	BRA	PXUN
PXDN:	STAA	E,BUMP
	LDAA	I,35			;"ALIGNED"
	JSR	E,MESOUT
	LDAA	I,33			;PUSH START2
	JSR	E,MESOUT
	LDAA	E,CN2
	BITA	I,100
	BNE	PXAL
	LDAA	E,CN2
	BITA	I,100
	BEQ	S,-5
	LDAA	I,34			;CLEAR "PUSH START2"
	JSR	E,MESOUT
	RTS
SWITCH:	LDAA	E,CN1
	COMA
	ANDA	I,300
	BEQ	S,+5
	STAA	E,SCORES
	LDAA	E,CN2
	COMA
	ANDA	I,300
	BEQ	S,+5
	STAA	E,SCORES
	STAA	E,CN2
	RTS
TSWIT:	LDAA	E,CN2
	BITA	I,20
	BEQ	S,3
	WAI
	RTS
START:STARTG:	SEI
	LDS	I,375
	LDAA	E,DIAG
	CMPA	I,17
	BNE	ST
	LDAA	E,DIAG+4
	CMPA	I,266
	BNE	ST
	JMP	E,DIAG
ST:	LDX 	I,330			;SET SP=370
ST1:	CLR	X,0
	DEX
	BNE	ST1
	LDAA I,60
	LDAB I,6
	LDX I,SCR1
	STAA X,0
	INX
	DECB
	BNE S,-4
ST2:	COM	E,AT
	CLR	E,GOFLAG
	CLR	E,COIN
	LDX	I,0
	STX	D,TIMER
	STX	D,TIMER+1
	CLR	E,BUMP
	CLR	E,SCRAT
	STAA E,STF12		;START 1&2 OFF
	LDAA	I,200
	STAA	D,PSYNC
	CLR	E,RTC
	JSR	E,BLANK
	JSR	E,PFOUT
	CLR	E,PFG
	JSR	E,BRACK
	LDAA I,16
	STAA D,ATTYM
ATLP:	LDAA E,CN2
	BITA	I,20
	BNE	ATLPZ
	JMP	E,TESTPS
ATLPZ:	COMA
	ANDA I,3
	STAA D,MODE
MD:	STAA E,STF12		;START 1&2 OFF
MD0:	LDAA D,MODE
	JSR E,MESOUT
	LDAA I,21
	JSR E,MESOUT
	LDAA E,CN1
	COMA
	ANDA I,3
	ADDA I,22
	JSR E,MESOUT
	STAA E,CN2
	LDAA D,GOTYM
	BNE ATON
	LDAA I,13
	STAA D,GOTYM
	COM E,GOFLAG
	LDAA D,GOFLAG
	BMI GOFF
	LDAA I,4
	BRA GOON		;GAME OVER TO SCREEN
GOFF:	LDAA I,5		;GAME OVER OFF SCREEN
GOON:	JSR E,MESOUT
ATON:	JSR E,ATPLY		; ATTRACT BALL MOTION 1 FRAME
	JSR E,COININ
	LDAA D,COIN
	BNE COINOP
	DEC E,GOTYM
	LDAA D,RTC
	BNE ATLP
	DEC E,ATTYM
	BNE ATLP
	WAI
COINOP:	LDAA D,MODE
	BNE MOD1
	LDAA D,COIN
	CMPA I,2
	BCC ST2M1
	STAA E,STN1		;START 1 ON
	STAA E,STF2		;START 2 OFF
	LDAA I,6		;PUSH START1 OR INSERT COIN
	JSR E,MESOUT
	CLR E,PNUM
CON:	JSR E,COININ
	LDAA I,2
	CMPA D,COIN
	BEQ ST2M1
	LDAA E,CN1
	ASLA
	BMI CON
	CLR E,CREDIT
	JMP E,GMPLAY
ST2M1:	STAA E,STF1		;START 1 OFF
	STAA E,STN2		;START 2 ON
	LDAA	I,7		;PUSH START 2
	JSR	E,MESOUT
	LDAA	I,200
	STAA	D,PNUM
	LDAA E,CN2
	ASLA
	BMI S,-4
	JMP E,GMPLAY
MOD1:	CMPA I,1
	BNE MOD2
MDRST1:	STAA E,STN12		;START 1&2 ON
	LDAA I,10		;PUSH START1 OR START2
	JSR E,MESOUT
MDSTLP:	LDAA E,CN1
	ASLA
	BPL M2ST1
	LDAA E,CN2
	ASLA
	BMI MDSTLP
	LDAA I,200
	BRA MD1GO
M2ST1:	CLRA
MD1GO:	CLR E,CREDIT
MDGO:	STAA D,PNUM
	JMP E,GMPLAY
MOD2:	CMPA I,2
	BNE MOD3
	STAA E,STN12		;START 1&2 ON
	LDAA I,10		;PUSH START1 OR START2
	JSR E,MESOUT
	LDAA I,1
	STAA E,CREDIT
MD2LP:	LDAA E,CN1
	ASLA
	BPL MD2ST1
	LDAA E,CN2
	ASLA
	BMI MD2LP
	LDAA I,200
	BRA MDGO
MD2ST1:	CLRA
	BRA MDGO
MOD3:	LDAA D,COIN
	CMPA I,2
	BGE MD2E
	LDAA I,11		;INSERT COIN
	JSR E,MESOUT
	JSR E,COININ
	BRA MOD3
MD2E:	STAA E,STN1		;START 1 ON
	LDAA I,6		;PUSH START1 OR INSERT COIN
	JSR E,MESOUT
MD23:	LDAA E,CN1
	ASLA
	BPL MD2G
	JSR E,COININ
	LDAA D,COIN
	CMPA I,3
	BLT MD23
	STAA E,STF1		;START 1 OFF
	LDAA I,11		;INSERT COIN
	JSR E,MESOUT
MD34L:	JSR E,COININ
	LDAA D,COIN
	CMPA I,4
	BLT MD34L
	STAA E,STN2		;START 2 ON
	LDAA I,7		;PUSH START 2
	JSR E,MESOUT
	BRA MD24
MD2G:	CLR E,PNUM
MD4LP:	CLR E,CREDIT
	JMP E,GMPLAY
MD24:	LDAA E,CN2
	ASLA
	BMI S,-4
	LDAA I,200
	STAA D,PNUM
	BRA MD4LP
GMPLAY:	CLR E,AT
	CLR E,PFG
	STAA E,STF12		;START 1&2 OFF
	LDAA I,200
	STAA E,PSYNC
	CLR E,RTC
	JSR E,BLANK
	JSR E,PFOUT
	CLR E,BP
	LDAA	E,CN1
	COMA
	ANDA	I,3
	ADDA	I,62
	STAA	D,RACK
	LDAA	I,60
	LDAB	I,6
	LDX	I,SCR1
	STAA	X,0
	INX
	DECB
	BNE	S,-4
	INCA
	STAA	D,HEAT
NEXTP:	CLR	E,BONUS
	LDAA	I,63
	STAA	D,TIMER
	LDAA	I,60
	STAA	D,TIMER+1
MAIN1:	JSR	E,BRACK
MAIN0:	LDAA	D,BP
	BEQ	NBP
	LSRA
	BCS	S,+7
	STAA	E,BUMP+1
	BRA	S,+5
	STAA	E,BUMP
	ROLA
	DECA
	STAA	D,BP
NBP:	CLR	E,SYNC
	LDAA	D,SYNC
	BPL	S,-2
	INC	E,RTC
	STAA	E,CN2
	LDAA	D,RTC
	ANDA	I,77
	BNE	MVB1
	LDAA	I,60
	CMPA	D,TIMER
	BNE	TUPDT
	CMPA	D,TIMER+1
	BNE	TUPDT
SRATST:LDAA D,PNUM
	BEQ HEATCK
	LDAA	D,PFG
	BMI	HEATCK
	LDAA	I,200
	STAA	D,PFG
	JMP	E,NEXTP
HEATCK:	LDAA	D,RACK
	CMPA	D,HEAT
	BNE	HTOK
	LDAA D,MODE
	CMPA I,2
	BEQ S,+5
	JMP E,ST2
	LDAA D,CREDIT
	BNE S,+5
	JMP E,ST2
	STAA	E,BUMP		;CLEAR BUMP SOUND
	JMP	E,MDRST1
HTOK:	INC	E,HEAT
	CLR	E,PFG
	JMP	E,NEXTP
TUPDT:	LDAA	D,TIMER+1
	DECA
	CMPA	I,57
	BNE	TMOK
	LDAA	I,71
	STAA	D,TIMER+1
	LDAA	D,TIMER
	DECA
	STAA	D,TIMER
	BRA	S,+4
TMOK:	STAA	D,TIMER+1
MVB1:	TST	E,ZCNT
	BNE	MVB2
	TST	E,EXPLY
	BPL	BONSET
BON:	JMP	E,MAIN1
BONSET:	LDAA	D,BONUS
	BMI	BON
	LDAA	I,200
	STAA	D,BONUS
	LDAB	I,1
	LDAA	I,5
	ADDA	D,TIMER+1
	CMPA	I,72
	BCC	1$
	STAA	D,TIMER+1
	BRA	2$
1$:	INCB
	SUBA	I,12
	STAA	D,TIMER+1
2$:	ADDB	D,TIMER
	STAB	D,TIMER
	BRA	BON
MVB2:	LDX	I,17
MVB0:	JSR	E,PLAY		;MOVE BALL # IN IR
	DEX
	BNE	MVB0
	LDAA	I,20
	STAA	D,CNTACT
	JSR	E,PLAY
	LDAA	D,PFG
	BPL	S,+7
	LDX	I,P2
	BRA	S,+5
	LDX	I,P1
	JSR	E,GETVAL
	STAA	D,R2		;H VAL
	STAB	D,R3		;V VAL
	LDX	I,XTAB
	STX	D,R6
	STX	D,R4
	LDAA	D,R5
	ADDA	D,R2
	STAA	D,R5
	LDAA	D,R4
	ADCA	I,0
	STAA	D,R4
	LDAA	D,R7
	ADDA	D,R3
	STAA	D,R7
	LDAA	D,R6
	ADCA	I,0
	STAA	D,R6
	LDX	D,R4
	LDAA	X,0
	STAA	D,OBJHV
	LDX	D,R6
	LDAA	X,0
	STAA	D,OBJVV
	JMP	E,MAIN0
;USED A&B: X-POT VALUE RETURNED IN A, Y-POT VALUE RETURNED IN B
GETVAL:	LDAA	I,10
	STAA	E,CONTRL
	LDAB	X,0
	BITB	I,10
	BNE	S,+3
	CLRA
	ORAA	I,4
	STAA	E,CONTRL
	LDAB	X,0
	BITB	I,10
	BNE	S,+4
	ANDA	I,10
	ORAA	I,2
	STAA	E,CONTRL
	LDAB	X,0
	BITB	I,10
	BNE	S,+4
	ANDA	I,14
	ORAA	I,1
	STAA	E,CONTRL
	LDAB	X,0
	BITB	I,10
	BNE	S,+4
	ANDA	I,16
	STAA	D,TEMP			;X SAVED
	LDAB	I,10
	STAB	E,CONTRL
	LDAA	X,0
	BITA	I,4
	BNE	S,+3
	CLRB
	ORAB	I,4
	STAB	E,CONTRL
	LDAA	X,0
	BITA	I,4
	BNE	S,+4
	ANDB	I,10
	ORAB	I,2
	STAB	E,CONTRL
	LDAA	X,0
	BITA	I,4
	BNE	S,+4
	ANDB	I,14
	ORAB	I,1
	STAB	E,CONTRL
	LDAA	X,0
	BITA	I,4
	BNE	S,+4
	ANDB	I,16
	LDAA	D,TEMP
	RTS

BRACK:	SEI
	STS	D,SPT
	LDS	I,BINIT-1	;INITIAL BALL TABLE
	LDX	I,200
	CLR	X,0
	DEX
	BNE	S,-3
	LDX	I,317			;SET CONTACT TBL TO NONE
	LDAB	I,20
	TBA
	STAA	X,0
	DEX
	DECB
	BNE	S,-4
	LDX	I,0
	LDAB	I,20
	STAB	D,R0
BLK0:	PULA
	LDAB	D,PFG
	BMI	S,+3
	NEGA
	STAA	X,0
	PULA
	STAA	X,20
	INX
	DEC	E,R0
	BNE	BLK0
	LDS	D,SPT
	TST	E,IRQRST
	CLI
	LDAA	I,17
	STAA	D,ZCNT
	RTS
BINIT:	.BYTE	60,237,220,237,236,230,236,247,254,220,254,237
	.BYTE	254,256,272,210,272,227,272,246,272,265
	.BYTE	310,200,310,217,310,236,310,255,310,274
XTAB:	.BYTE 70,60,50,40,30,20,10,0,0,370,360,350,340,330,320,310
BLANK:	LDX	I,DISP
	LDAB	I,40
	STAB	D,R0
BLKLP:	CLR	E,SYNC
	CLR	E,PSYNC
	CLI
	LDAA	D,PSYNC
	BPL	S,-2
	STAA	E,CN2
BLKLP1:	CLR	X,0
	INX
	DECB
	BNE	BLKLP1
	LDAB	I,40
	DEC	E,R0
	BNE	BLKLP
	RTS
				;THE BASIC MOVE AND CHECK ROUTINE
MESOUT:	ASLA
	LDX I,MIN
 	STX D,MORA
	TAB
	ADDA D,MORA+1
	STAA D,MORA+1
	LDX I,MOUT
	STX D,MORD
	ADDB D,MORD+1
	STAB D,MORD+1
	LDX D,MORA
	LDX X,0
	STX D,MORA
	LDX D,MORD
	LDX X,0
	STX D,MORD
MSOUT1:	CLR E,SYNC
	CLR E,PSYNC
	CLI
MSOUT2:	LDAA E,CN1
	ANDA E,CN2
	BMI MSOUT3
	JSR E,COININ
	BRA MSOUT1
MSOUT3:	LDAA D,PSYNC
	BPL MSOUT2
MESLP:	LDX D,MORA
	LDAA X,0
	BMI MRORE
	TAB
	CLRA
MESLOP:	INX
	STX D,MORA
	LDX D,MORD
MDRLP:	STAB X,0
	INX
	DECA
	BPL MDRLP
	STX D,MORD
	BRA MESLP
MRORE:	COMA
	BNE MESLOP
	STAA	E,CN2
	RTS
ATPLY:	CLR E,SYNC
CKCON:	LDAA E,CN1
	ANDA E,CN2
	BMI DOSYNK
	JSR E,COININ
DOSYNK:	LDAA D,SYNC
	BPL CKCON
	INC E,RTC
	STAA	E,CN2
	RTS
COININ:	LDAA	D,AT
	BMI	S,+3
	RTS
	LDAA	E,CN1
	ANDA	E,CN2
	BPL	S,+3
	RTS
	LDAA	I,17
	STAA	D,R0
	STAA	D,R1
CNLP:	LDAA	E,CN1
	BMI	C1NT
	LDAA	D,R0
	BEQ	C2CK
C1CK:	DEC	E,R0
	BNE	C2CK
	INC	E,COIN
	BRA	CNLP0
C1NT:	LDAA	I,17
	STAA	D,R0
C2CK:	LDAA	I,143		;1 MSEC. DELAY
	STAA	D,R4
	DEC	E,R4
	BNE	S,-3
	LDAA	E,CN2
	BMI	C2NT
	LDAA	D,R1
	BEQ	CNLP0
C2PRC:	DEC	E,R1
	BNE	CNLP0
	INC	E,COIN
	BRA	CNLP0
C2NT:	LDAA	I,17
	STAA	D,R1
CNLP0:	LDAA	E,CN1
	ANDA	E,CN2
	BPL	CNLP
	RTS


; THIS ROUTINE TAKES X AND Y IN A AND B, RETURNS THETA AND R
POLAR:	STAA	D,K		;SAVE X
	CLRA			;START WITH QUADRANT 0
	EORB	D,K
	BPL 	S,+4	; --	 IF X AND Y ARE SAME SIGN, QUAD. IS 0 OR 2
	LDAA	I,20	;  !	 OTHERWISE, QUAD IS 1 OR 3 (SHIFTED LEFT 4)
	EORB	D,K	; <-	 RECONSTITUTE Y
	BPL	S,+5	; --
	NEGB		;  !	 TAKE !Y!
	ADDA	I,40	;  !	 ADD 2 TO QUAD. OF Y WAS NEG.
	STAA	D,L	; <-	 SAVE QUAD. FOR NOW
	LDAA	D,K
	BPL	S,+3
	NEGA			;TAKE !X!
	STAA	D,R0
	ASLA
	ASLA
	ASLA			;MAKE ROOM FOR Y
	ASLA
	SUBA	D,R0
	ABA			;AND STUFF IT
	STX	D,X3
	LDAB	I,TBL1&177400/400 ;TABLE SHOULD LIVE ON 256 BYTE BOUNDARY, TO
	STAB	D,TPTR		; SAVE 8 BYTES, 20 CYCLES
	STAA	D,TPTR+1
	LDX	D,TPTR
	LDAA	X,0
	TAB
	BEQ	PLR
	ANDB	I,17		;ISOLATE THETA (BOTTOM FOUR BITS)
	BNE	PLR
	TST	E,R0
	BNE	PLR
	LDAB	I,20
PLR:	ANDA	I,360		;ISOLATE R  (TOP FOUR BITS)
	STAA	D,R1		;SAVE R
	LDAA	D,L
	BITA	I,20		;CHECK QUAD 1 OR 3
	BEQ	S,+5	; --
	ADDA	I,20	;  !	 IF SO ANGLE IS NEXT QUAD
	NEGB		;  !	 MINUS THETA
	ABA		; <-
	ANDA	I,77
	TAB
	LDAA	D,R1		;GET R
	LSRA
	LSRA
	LSRA
	LSRA
	LDX	D,X3
	RTS

;THESE TAKE AN ANGLE IN A, RETURN SIN OR COS IN A. CLOBBER B, TPTR+1,X3
SIN:	SUBA	I,20		;SIN (ALPHA)=COS (PI/2-ALPHA)
	NEGA
COS:	TAB
	ANDA	I,17		;ISOLATE ANGLE MOD PI/2
	BITB	I,20		;CHECK IF QUADRANT 1 OR 3
	BEQ	EQUAD
	TSTA
	BNE	S,+6
	LDAA	I,20
	BRA	EQUAD
	NEGA			;ALPHA<- PI -ALPHA
	ANDA	I,17
EQUAD:	STAA	D,TPTR+1
	STX	D,X3
	LDX	D,TPTR
	LDAA	X,TBL2-TBL1
	ADDB	I,20
	BITB	I,40		;CHECK IF QUAD 1 OR 2
	BEQ	S,+3
	NEGA
	LDX	D,X3
	RTS

INT:	LDAA	D,SYNC
	BPL	DISP1
INTA:	LDAA	E,IRQRST
	LDAA	D,PSYNC
	BMI	S,+5
	COM	E,PSYNC
	RTI
DISP1:	LDAA	D,PSYNC
	BMI	DISP2
	COM	E,PSYNC
	BRA	INTA
DISP2:	STS	D,SPT		;ENTERED ON INTERRUPT FROM VBLANK
	LDS	I,-1		;GET READY TO PUL LOC 0
	LDX	I,HPOS		;POINT TO DISP. MOVING OBJECTS RAM.
	LDAA	I,20		;# OF WDS TO MOVE
DOLOOP:	PULB
	STAB	X,0
	INX
	DECA
	BNE 	DOLOOP
	LDX	I,VPOS
	LDAA	I,20
DOVLP:	PULB
	STAB	X,0
	INX
	DECA
	BNE	DOVLP
	LDS	I,SCR1-1
	PULA
	STAA	E,DISP+203
	PULA
	STAA	E,DISP+204
	PULA
	STAA	E,DISP+230
	PULA
	STAA	E,DISP+231
	PULA
	STAA	E,DISP+324
	PULA
	STAA	E,DISP+325
	PULA
	STAA	E,DISP+312
	LDS	D,SPT
	TST	E,AT
	BPL	2$
	JMP	E,INTEND
2$:	LDX	I,DISP+1706
	LDAA	D,RTC
	TST	E,PFG
	BMI	P2AROW
	BITA	I,20
	BNE	P1AOFF
	LDAA	I,32		;P1 ARROW ON
	LDAB	I,33
	STAA	X,1
	STAA	X,2
	STAB	X,41
	STAB	X,42
	LDAA	D,BONUS
	BPL	1$
	LDAA	I,2		;B
	LDAB	I,17		;O
	STAA	X,3
	STAB	X,4
	LDAA	I,16		;N
	LDAB	I,25		;U
	STAA	X,5
	STAB	X,6
	LDAA	I,23		;S
	STAA	X,7
1$:	LDAA	I,21
	LDAB	I,12
	BRA	P1AON
P1AOFF:	CLR	X,1
	CLR	X,2
	CLR	X,41
	CLR	X,42
	CLR	X,3
	CLR	X,4
	CLR	X,5
	CLR	X,6
	CLR	X,7
	CLRA
	CLRB
P1AON:	STAA	X,0
	STAB	X,40
	CLR	X,21
	CLR	X,22
	CLR	X,23
	CLR	X,61
	CLR	X,62
	CLR	X,63
	CLR	X,14
	CLR	X,15
	CLR	X,16
	CLR	X,17
	CLR	X,20
	BRA	INTEND
P2AROW:	BITA	I,20
	BNE	P2AOFF
	LDAA	I,32
	LDAB	I,33
	STAA	X,21
	STAB	X,61
	STAA	X,22
	STAB	X,62
	LDAA	D,BONUS
	BPL	1$
	LDAA	I,2
	LDAB	I,17
	STAA	X,14
	STAB	X,15
	LDAA	I,16
	LDAB	I,25
	STAA	X,16
	STAB	X,17
	LDAA	I,23
	STAA	X,20
1$:	LDAA	I,27
	LDAB	I,30
	BRA	P2AON
P2AOFF:	CLR	X,21
	CLR	X,22
	CLR	X,61
	CLR	X,62
	CLR	X,14
	CLR	X,15
	CLR	X,16
	CLR	X,17
	CLR	X,20
	CLRA
	CLRB
P2AON:	STAA	X,23
	STAB	X,63
	CLR	X,0
	CLR	X,1
	CLR	X,2
	CLR	X,40
	CLR	X,41
	CLR	X,42
	CLR	X,3
	CLR	X,4
	CLR	X,5
	CLR	X,6
	CLR	X,7
INTEND:	COM	E,SYNC
	LDAA	E,IRQRST
	RTI			;REMEMBER, VBLANK MAY STILL BE THERE,SO
;				 BE CAREFUL ABOUT READING SWITCHES RIGHT AWAY.
DIV32:	CLRA
	ASLB
	BCC	S,+3
	COMA			;SIGN EXTEND
	ASLB
	ROLA
	ASLB
	ROLA
	RTS

PFOUT:	SEI
	LDX	I,DISP+140
	STX	D,MORA
	LDX	I,PLYFLD
	STX 	D,MORD
PFOUT1:	CLR	E,SYNC
	CLR	E,PSYNC
	CLI
	LDAA	D,PSYNC
	BPL	S,-2
	STAA	E,CN2
DELOOP:	LDX	D,MORD
	LDAA	X,0
	BMI	RORE
	TAB
	CLRA
DRLOOP:	INX
	STX	D,MORD
	LDX	D,MORA
DRLP:	STAB	X,0
	INX
	DECA
	BPL	DRLP
	STX	D,MORA
	BRA	DELOOP
RORE:	COMA
	BNE	DRLOOP
DOUT:	INX
	STX	D,MORD
	CPX	I,PFEND
	BNE	PFOUT1
	RTS
FILR	= 73776-.
	.REPT FILR
	.BYTE 0
	.ENDR
	.=73776
	.BYTE 226,0		;CHECKSUM & COMPLEMENT
	.BYTE 64,0

;BEING THE TABLES FOR GENERATING PLAYFIELD
;AS INTERPRETED BY THE DISPLAY ROUTINE, -N MEANS REPEAT PREVIOUS CHARACTER
;N TIMES THUS .BYTE  50,-5 WILL PRODUCE 6 OF PLAYFIELD GRAPHIC #50. A REPEAT
;COUNT OF 1 (IE. A -1) IS INTERPRETED AS THE END OF THE STRING, CAUSES THE
;DISPLAY ROUTINE TO RETURN
;	!!! THESE ASSUME A 32 X 32 PLAYFIELD, MUST BE UPDATED !!!
;
PLYFLD:	.BYTE	0,20,14,1,31,5,22,0,61,0,-12.,20,14,1,31,5,22,0,62,0,0,-1	;3
	.BYTE	0,-31.,-1							;4
	.BYTE	0,-8.,22,1,3,13,0,53,51,51,52,0,24,11,15,5,0,-8.,-1			;5
	.BYTE	0,53,51,51,52,0,-8.,50,0,0,50,0,-8.,53,51,51,52,0,-1		;6
	.BYTE	0,50,0,0,55,51,-8.,74,75,76,77,51,-8.,54,0,0,50,0,-1		;7
	.BYTE	0,50,0,35,36,0,-21.,37,40,0,50,0,-1						;10
	.BYTE	0,55,52,34,0,-23.,41,53,54,0,-1					;11
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;12
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;13
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;14
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;15
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;16
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;17
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;20
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;21
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;22
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;23
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;24
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;25
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;26
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;27
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;30
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;31
	.BYTE	0,0,50,0,-25.,50,0,0,-1						;32
	.BYTE	0,53,54,42,0,-23.,47,55,52,0,-1					;33
	.BYTE	0,50,0,43,44,0,-21.,45,46,0,50,0,-1						;34
	.BYTE	0,50,0,0,53,51,-8.,56,57,72,73,51,-8.,52,0,0,50,0,-1		;35
	.BYTE	0,55,51,51,54,0,-8.,50,0,0,50,0,-8.,55,51,51,54,0,-1		;36
	.BYTE	0,-13.,55,51,51,54,0,-13.,-1					;37
PFEND:	.BYTE	0
MIN:	.BYTE	MES0&177400/400,MES0&377
	.BYTE	MES1&177400/400,MES1&377
	.BYTE	MES2&177400/400,MES2&377
	.BYTE	MES3&177400/400,MES3&377
	.BYTE	MES4&177400/400,MES4&377
	.BYTE	MES5&177400/400,MES5&377
	.BYTE	MES6&177400/400,MES6&377
	.BYTE	MES7&177400/400,MES7&377
	.BYTE	MES10&177400/400,MES10&377
	.BYTE	MES11&177400/400,MES11&377
	.BYTE	MES12&177400/400,MES12&377
	.BYTE	MES13&177400/400,MES13&377
	.BYTE	MES14&177400/400,MES14&377
	.BYTE	MES15&177400/400,MES15&377
	.BYTE	MES16&177400/400,MES16&377
	.BYTE	MES17&177400/400,MES17&377
	.BYTE	MES20&177400/400,MES20&377
	.BYTE	MES21&177400/400,MES21&377
	.BYTE	MES22&177400/400,MES22&377
	.BYTE	MES23&177400/400,MES23&377
	.BYTE	MES24&177400/400,MES24&377
	.BYTE	MES25&177400/400,MES25&377
	.BYTE	MES26&177400/400,MES26&377
	.BYTE	MES27&177400/400,MES27&377
	.BYTE	MES30&177400/400,MES30&377
	.BYTE	MES31&177400/400,MES31&377
	.BYTE	MES32&177400/400,MES32&377
	.BYTE	MES33&177400/400,MES33&377
	.BYTE	MES34&177400/400,MES34&377
	.BYTE	MES35&177400/400,MES35&377
	.BYTE	MES36&177400/400,MES36&377
	.BYTE	MES37&177400/400,MES37&377
	.BYTE	MES40&177400/400,MES40&377
MOUT:	.BYTE	DISP+1705&177400/400,DISP+1705&377		;0
	.BYTE	DISP+1705&177400/400,DISP+1705&377		;1
	.BYTE	DISP+1705&177400/400,DISP+1705&377		;2
	.BYTE	DISP+1705&177400/400,DISP+1705&377		;3
	.BYTE	DISP+613&177400/400,DISP+613&377		;4
	.BYTE	DISP+613&177400/400,DISP+613&377		;5
	.BYTE	DISP+603&177400/400,DISP+603&377		;6
	.BYTE	DISP+603&177400/400,DISP+603&377		;7
	.BYTE	DISP+603&177400/400,DISP+603&377		;10
	.BYTE	DISP+603&177400/400,DISP+603&377		;11
	.BYTE	DISP+721&177400/400,DISP+721&377		;12
	.BYTE	DISP+713&177400/400,DISP+713&377		;13
	.BYTE	DISP+721&177400/400,DISP+721&377		;14
	.BYTE	DISP+722&177400/400,DISP+721&377		;15
	.BYTE	DISP+723&177400/400,DISP+721&377		;16
	.BYTE	DISP+724&177400/400,DISP+721&377		;17
	.BYTE	DISP+725&177400/400,DISP+721&377		;20
	.BYTE	DISP+1554&177400/400,DISP+1554&377		;21
	.BYTE	DISP+1553&177400/400,DISP+1553&377		;22
	.BYTE	DISP+1553&177400/400,DISP+1553&377		;23
	.BYTE	DISP+1553&177400/400,DISP+1553&377		;24
	.BYTE	DISP+1553&177400/400,DISP+1553&377		;25
	.BYTE	DISP+1147&177400/400,DISP+1147&377		;26
	.BYTE	DISP+1153&177400/400,DISP+1153&377		;27
	.BYTE	DISP+1153&177400/400,DISP+1153&377		;30
	.BYTE	DISP+1153&177400/400,DISP+1153&377		;31
	.BYTE	DISP+1153&177400/400,DISP+1153&377		;32
	.BYTE	DISP+1342&177400/400,DISP+1342&377		;33
	.BYTE	DISP+1340&177400/400,DISP+1340&377		;34
	.BYTE	DISP+1157&177400/400,DISP+1157&377		;35
	.BYTE	DISP+1157&177400/400,DISP+1157&377		;36
	.BYTE	DISP+726&177400/400,DISP+721&377		;37
	.BYTE	DISP+1147&177400/400,DISP+1147&377		;40
MES0:	.BYTE	0,61,0,3,17,11,16,0,0,50,0,0,50,0,0,5,1,3,10,0,-2.,-1
MES1:	.BYTE	0,61,0,3,17,11,16,0,0,50,0,0,50,0,0,61,0,7,1,15,5,0,-1
MES2:	.BYTE	0,61,0,3,17,11,16,0,0,50,0,0,50,0,62,0,7,1,15,5,23,0,-1
MES3:	.BYTE	0,62,0,3,17,11,16,23,0,50,0,0,50,0,0,5,1,3,10,0,-2,-1
MES4:	.BYTE	7,1,15,5,0,17,26,5,22,-1
MES5:	.BYTE	0,-8.,-1
MES6:	.BYTE	20,25,23,10,0,23,24,1,22,24,61,0,17,22,0,11,16,23,5,22,24,0,3,17,11,16,-1
MES7:	.BYTE	0,-7.,20,25,23,10,0,23,24,1,22,24,62,0,-6.,-1
MES10:	.BYTE	0,0,20,25,23,10,0,23,24,1,22,24,61,0,17,22,0,23,24,1,22,24,62,0,0,0,-1
MES11:	.BYTE	0,-7.,11,16,23,5,22,24,0,3,17,11,16,0,-6.,-1
MES12:	.BYTE	17,13,-1
MES13:	.BYTE	24,5,23,24,0,-1
MES14:	.BYTE	1,-1
MES15:	.BYTE	2,-1
MES16:	.BYTE	3,-1
MES17:	.BYTE	4,-1
MES20:	.BYTE	5,-1
MES21:	.BYTE	0,22,1,3,13,0,7,1,15,5,-1
MES22:	.BYTE	62,-1
MES23:	.BYTE	63,-1
MES24:	.BYTE	64,-1
MES25:	.BYTE	65,-1
MES26:	.BYTE	20,17,24,0,-1
MES27:	.BYTE	20,61,-1
MES30:	.BYTE	20,62,-1
MES31:	.BYTE	20,63,-1
MES32:	.BYTE	20,64,-1
MES33:	.BYTE	20,25,23,10,0,23,24,1,22,24,62,-1
MES34:	.BYTE	0,-15.,-1
MES35:	.BYTE	1,14,11,7,16,5,4,0,-3.,-1
MES36:	.BYTE	16,17,24,0,1,14,11,7,16,5,4,-1
MES37:	.BYTE	6,-1
MES40:	.BYTE	24,5,23,24,0,3,17,15,20,14,5,24,5,-1
	FIL	=76000-.
	.REPT	FIL
	.BYTE	0
	.ENDR
	.=76000
	.BYTE 317,0		;CHECKSUM & COMPLEMENT

PLAY:	LDAA	X,OBJHV		;X POINTS TO BALL UNDER CONSIDERATION
	TAB
	ORAA	X,OBJVV
	BNE	MANDD
	RTS			;IF BALL IS NOT MOVING, RETURN
MANDD:	LDAA	X,OBJV
	CMPA	I,114		;TOP LINE
	BCS	IVRTA
	CMPA	I,353		;BOTTOM LINE
	BCC	IVRTA
	LDAA	X,OBJH
	CMPA	I,37		;RIGHT SIDE
	BCS	IVRT
	CMPA	I,345		;LEFT SIDE
	BCC	IVRTL
	JMP	E,WALCKE
IVRT:	LDAA	X,OBJV
	CMPA	I,122
	BCS	SCORE
	CMPA	I,345
	BCC	SCORE
IVRTR:	LDAA	X,OBJHV
	BMI	NH
	JMP	E,WALCKE
NH:	NEG	X,OBJHV
	NEG	X,OBJHVL
	BRA	BMP
IVRTL:	LDAA	X,OBJV
	CMPA	I,122
	BCS	SCORE
	CMPA	I,345
	BCC	SCORE
IVTL:	LDAA	X,OBJHV
	BPL	NH
	JMP	E,WALCKE
IVRTA:	LDAA	X,OBJH
	CMPA	I,43
	BCS	SCORE
	CMPA	I,172
	BCS	IVRTAA
	CMPA	I,211
	BCS	IVZ
	CMPA	I,337
	BCC	SCORE
IVRTAA:	LDAA	X,OBJV
	CMPA	I,114
	BCC	TP
	LDAA	X,OBJVV
	BMI	IV
	JMP	E,WALCKE
IVZ:	LDAA	X,OBJV
	CMPA	I,110
	BCS	SCORE
	CMPA	I,357
	BCC	SCORE
	JMP	E,WALCKE
IV:	NEG	X,OBJVV
	NEG	X,OBJVVL
BMP:	CPX	I,0
	BEQ	BMPN
	INC	E,BP
	INC	E,BP
BMPN:	LDAA	I,20
	STAA	X,CNTACT
	BRA	WALCKE
TP:	LDAA	X,OBJVV
	BPL	IV
	BRA	WALCKE
SCORE:	CPX	I,0
	BEQ	SCACH
	STAA	E,SCORES
	DEC	E,ZCNT
	LDAA	I,10
	STAA	X,OBJH
	STAA	X,OBJV
	CLR	X,OBJHL
	CLR	X,OBJVL
	CLR	X,OBJVV
	CLR	X,OBJHV
	STX	D,X4
	LDX	I,SCR1
	LDAA	D,PFG
	BPL	S,+4
	INX
	INX
	LDAA	X,1
	INCA
	CMPA	I,72
	BNE	SCOK
	LDAA	I,60
	STAA	X,1
	LDAA	X,0
	INCA
	CMPA	I,72
	BNE	S,+4
	LDAA	I,60
	STAA	X,0
	LDX	D,X4
	RTS
SCOK:	STAA	X,1
	BRA	S,-5
SCACH:	LDAA	I,20
	STAA	D,R1
LP2:	LDAA	I,100
	STAA	D,R0
	LDAA	I,0
LP3:	LDAB	I,377
LP:	BITA	I,1
	BNE	S,+7
	STAA	E,SCRAT
	BRA	S,+5
	STAA	E,SCRAT+1
	DECB
	BNE	S,-1
	DECA
	STAA	E,CN2
	DEC	E,R0
	BNE	LP3
	DEC	E,R1
	BEQ	S,+10
	LDAB	I,40
	STAB	D,R0
	BRA	LP
	PULA
	PULA
	JMP	E,SRATST
WALCKE:	LDAB	X,OBJHV
	STAB	D,HOLDHV
	JSR	E,DIV32
	STAA	D,TSTHV	
	STAB	D,TSTHVL	;SAVE DIVIDED VELOCITY FOR DECEL
	ADDB	X,OBJHL
	ADCA	X,OBJH
	STAB	X,OBJHL
	STAA	X,OBJH		;UPDATE HORIX POSN.
	STAA	D,TSTH		;SAVE FOR COLLISION CHECK
	LDAB	X,OBJVV
	STAB	D,HOLDVV
	JSR	E,DIV32
	STAA	D,TSTVV
	STAB	D,TSTVVL
	ADDB	X,OBJVL
	ADCA	X,OBJV
	STAB	X,OBJVL
	STAA	X,OBJV		;UPDATE VERT. POSN.
	STAA	D,TSTV		;SAVE FOR COLLISION CHECK
	STX	D,X2
	LDAB	D,X2+1
	EORB	D,TIME
	ANDB	I,3
	BNE	COLCHK
DECEL:	LDAA	X,OBJHV		;EACH BALL IS DECEL EACH FRAME
	LDAB	X,OBJHVL
	SUBB	D,TSTHVL
	SBCA	D,TSTHV
	BNE	S,+3
	CLRB
	STAA	X,OBJHV
	STAB	X,OBJHVL
	LDAA	X,OBJVV
	LDAB	X,OBJVVL
	SUBB	D,TSTVVL
	SBCA	D,TSTVV
	BNE	S,+3
	CLRB
	STAA	X,OBJVV
	STAB	X,OBJVVL
COLCHK:	LDX	I,17
	LDAB	D,TSTH
CHKLP:	TBA
	SUBA	X,OBJH		;ONE OFF TO ALLOW DEX, BNE END TEST.
	BPL	S,+3
	NEGA
	CMPA	I,15		;FOR 8X8=4
	BLS	CHKV
CNEXT:	CPX	I,0
	BEQ	CNOT
	DEX
	BRA	CHKLP
CNOT:	LDX	D,X2		;STRIKER
	RTS
CHKV:	CPX	D,X2		;SEE IF YOU ARE ME
	BEQ	CNEXT
	STX	D,R8		;TEST LAST CONTACT
	LDX	D,X2		;STRIKER
	LDAA	X,CNTACT	;LAST HIT FOR STRIKER
	LDX	D,R8		;CURRENT CONTACT #
	CMPA	D,R9		;TEST FOR SAME AS LAST HIT
	BEQ	CNEXT		;YES SAME
	LDAA	I,20
	STAA	X,CNTACT
	LDX	D,X2
	STAA	X,CNTACT
	LDX	D,R8
	STAA	D,CNTACT
	LDAA	D,TSTV
	SUBA	X,OBJV
	TAB
	BPL	S,+3
	NEGA
	CMPA	I,15		;FOR 8X8=4
	BLS	FINCK
CRET:	LDAB	D,TSTH
	BRA	CNEXT
CKSLOW:	TSTA
	BNE	CRET
	CLRB
	INCA
	BRA	SLORET
				;FINAL SCREEN AND HANDLE COLLISION
FINCK:	LDAA	D,TSTH
	SUBA	X,OBJH
	JSR	E,POLAR		;CONVERT RECT. TO POLAR
	CMPA	I,16		;FOR 8X8=11
	BGT	CRET
	STAB	D,THETA		;SAVE CONTACT ANGLE
NOSLW:	LDAB	X,OBJVV
	JSR	E,DIV32
	SUBB	D,TSTVVL
	SBCA	D,TSTVV		;COMPUTE V CLOSING VELOCITY
	NEGA
	PSHA			;SAVE FOR LATER
	LDAB	X,OBJHV
	JSR	E,DIV32
	SUBB	D,TSTHVL
	SBCA	D,TSTHV
	NEGA
	PULB			;NOW IS LATER
	JSR	E,POLAR
REENT:	SUBB	D,THETA		;CLOSING ANGLE - CONTACT ANGLE
	BPL	S,+3
	NEGB
	CMPB	I,20
	BCS	CKSLOW		;THEY ARE MOVING AWAY FROM CONTACT
SLORET:	STAA	D,J		;SAVE R
	TBA
	JSR	E,COS
	LDAB	D,J
	ORAB	I,20		;SET SENTINAL
	JSR	E,MUL
	ANDA	I,17
	STAA	D,J		;ENERGY TO TRANSFER
	LDAA	D,THETA
	SUBA	I,40		;THETA-180
	ANDA	I,77
	STAA	D,THETA
	JSR	E,COS
	LDAB	D,J
	ORAB	I,20
	JSR	E,MUL		;AMOUNT OF ENERGY ADD -> STRUCK SUB -> HITTER
	TSTA
	BNE ADMY
	CLRB			;STRIKEN
	STX	D,X3		;SAVE STRIKEN
	LDX	D,X2		;STRIKER
	LDAA	X,OBJHV		;STRIKER
	STAB	X,OBJHV		;TO STRIKER
	CLRB
	JSR	E,CNTCT
	LDX	D,X3
	LDAA	D,X2+1
	BNE	S,+4
	LDAA	I,20
	STAA	X,CNTACT
	BRA	ADOKA
ADMY:	TAB
	ADDA	X,OBJHV
ADOKA:	STAA	X,OBJHV		;RETURN H-VEL
	CLR	X,OBJHVL
	STAB	D,L		;SAVE AMOUNT OF H-ENERGY
	LDAA	D,THETA
	JSR	E,SIN
	LDAB	D,J
	ORAB	I,20
	BSR	MUL
	TSTA
	BNE ADMYZ
	CLRB			;STRIKEN
	STX	D,X3
	LDX	D,X2
	LDAA	X,OBJVV		;STRIKER
	STAB	X,OBJVV		;TO STRIKER
	CLRB
	JSR	E,CNTCT
	LDX	D,X3
	LDAA	D,X2+1
	BNE	S,+4
	LDAA	I,20
	STAA	X,CNTACT
	BRA	ADOKB
ADMYZ:	TAB
	ADDA	X,OBJVV
ADOKB:	STAA	X,OBJVV		;RETURN VERT VEL
	CLR	X,OBJVVL
	STX	D,X3
	LDX	D,X2		;STRIKER ADDR TO IR
	LDAA	X,OBJVV
	SBA
SBOKZ:	STAA	X,OBJVV
	CLR	X,OBJVVL
	TAB
	JSR	E,DIV32
	STAA	D,TSTVV
	STAB	D,TSTVVL
	LDAA	X,OBJHV
	SUBA	D,L
SBOKW:	STAA	X,OBJHV
	CLR	X,OBJHVL
	TAB
	JSR	E,DIV32
	STAA	D,TSTHV
	STAB	D,TSTHVL
	LDX	D,X3
	STAA	E,CLICK
HITNO:	JMP	E,CRET
MUL:	STAA	D,K
	CLRA
MLP:	ASRA
	LSRB
	BEQ	MLPOUT
	BCC	MLP
	ADDA	D,K
	BRA	MLP
MLPOUT:	RTS
CNTCT:	LDAA	D,X3+1		;STRIKEN #
	BEQ	CNTQ
CNTCTA:	STAA	X,CNTACT
	LDAA	I,20
	STAA	D,CNTACT
	RTS
CNTQ:	LDAA	I,20
	BRA	CNTCTA
FILL	=TBL1-.
	.REPT	FILL
	.BYTE	0
	.ENDR
	.=77400			;LAST PAGE OF 4TH K
TBL1:	.BYTE	0
	.IRP	A,<<1,0>,<2,0>,<3,0>,<4,0>,<5,0>,<6,0>,<7,0>,<8,0>>
	PAIR	A
	.ENDM
	.IRP	A,<<9,0>,<10,0>,<11,0>,<12,0>,<13,0>,<14,0>>
	PAIR	A
	.ENDM
	.IRP	A,<<1,0>,<1,8>,<2,11>,<3,13>,<4,14>,<5,14>,<6,14>,<7,15>,<8,15>>
	PAIR	A
	.ENDM
	.IRP	A,<<9,15>,<10,15>,<11,15>,<12,15>,<13,15>,<14,15>>
	PAIR	A
	.ENDM
	.IRP	A,<<2,0>,<2,5>,<3,8>,<4,10>,<4,11>,<5,12>,<6,13>,<7,13>,<8,14>>
	PAIR	A
	.ENDM
	.IRP	A,<<9,14>,<10,14>,<11,14>,<12,14>,<13,14>,<14,15>>
	PAIR	A
	.ENDM
	.IRP	A,<<3,0>,<3,3>,<4,6>,<4,8>,<5,9>,<6,10>,<7,11>,<8,12>,<9,12>>
	PAIR	A
	.ENDM
	.IRP	A,<<9,13>,<10,13>,<11,13>,<12,14>,<13,14>,<14,14>>
	PAIR	A
	.ENDM
	.IRP	A,<<4,0>,<4,2>,<4,5>,<5,7>,<6,8>,<6,9>,<7,10>,<8,11>,<9,11>>
	PAIR	A
	.ENDM
	.IRP	A,<<10,12>,<11,12>,<12,12>,<13,13>,<14,13>,<15,13>>
	PAIR	A
	.ENDM
	.IRP	A,<<5,0>,<5,2>,<5,4>,<6,6>,<6,7>,<7,8>,<8,9>,<9,10>,<9,10>>
	PAIR	A
	.ENDM
	.IRP	A,<<10,11>,<11,11>,<12,12>,<13,12>,<14,12>,<15,13>>
	PAIR	A
	.ENDM
	.IRP	A,<<6,0>,<6,2>,<6,3>,<7,5>,<7,6>,<8,7>,<8,8>,<9,9>,<10,9>>
	PAIR	A
	.ENDM
	.IRP	A,<<11,10>,<12,10>,<13,11>,<13,11>,<14,12>,<15,12>>
	PAIR	A
	.ENDM
	.IRP	A,<<7,0>,<7,1>,<7,3>,<8,4>,<8,5>,<9,6>,<9,7>,<10,8>,<11,9>>
	PAIR	A
	.ENDM
	.IRP	A,<<11,9>,<12,10>,<13,10>,<14,11>,<15,11>,<15,11>>
	PAIR	A
	.ENDM
	.IRP	A,<<8,0>,<8,1>,<8,2>,<9,4>,<9,5>,<9,6>,<10,6>,<11,7>,<11,8>>
	PAIR	A
	.ENDM
	.IRP	A,<<12,9>,<13,9>,<14,10>,<14,10>,<15,10>,<15,11>>
	PAIR	A
	.ENDM
	.IRP	A,<<9,0>,<9,1>,<9,2>,<9,3>,<10,4>,<10,5>,<11,6>,<11,7>>
	PAIR	A
	.ENDM
	.IRP	A,<<12,7>,<13,8>,<13,9>,<14,9>,<15,9>,<15,10>,<15,10>>
	PAIR	A
	.ENDM
	.IRP	A,<<10,0>,<10,1>,<10,2>,<10,3>,<11,4>,<11,5>,<12,6>,<12,6>>
	PAIR	A
	.ENDM
	.IRP	A,<<13,7>,<13,7>,<14,8>,<15,8>,<15,9>,<15,9>,<15,10>>
	PAIR	A
	.ENDM
	.IRP	A,<<11,0>,<11,1>,<11,2>,<11,3>,<12,4>,<12,4>,<13,5>,<13,6>>
	PAIR	A
	.ENDM
	.IRP	A,<<14,6>,<14,7>,<15,8>,<15,8>,<15,8>,<15,9>,<15,9>>
	PAIR	A
	.ENDM
	.IRP	A,<<12,0>,<12,1>,<12,2>,<12,2>,<13,3>,<13,4>,<13,5>,<14,5>>
	PAIR	A
	.ENDM
	.IRP	A,<<14,6>,<15,7>,<15,7>,<15,8>,<15,8>,<15,8>,<15,9>>
	PAIR	A
	.ENDM
	.IRP	A,<<13,0>,<13,1>,<13,2>,<13,2>,<14,3>,<14,4>,<14,4>,<15,5>>
	PAIR	A
	.ENDM
	.IRP	A,<<15,6>,<15,6>,<15,7>,<15,7>,<15,7>,<15,8>,<15,8>>
	PAIR	A
	.ENDM
	.IRP	A,<<14,0>,<14,1>,<14,1>,<14,2>,<15,3>,<15,3>,<15,4>,<15,5>>
	PAIR	A
	.ENDM
	.IRP	A,<<15,5>,<15,6>,<15,6>,<15,7>,<15,7>,<15,8>,<15,8>>
	PAIR	A
	.ENDM
	.RADIX	10
TBL2:	.BYTE	63,63,63,61,59,56,53,49,45,40,35,30,24,18,12,6,0
LABL:	.BYTE	0,0,0,0,0,0
	.RADIX
	.BYTE	INT&177400/400,INT&377
	.BYTE	STARTG&177400/400,STARTG&377
	.BYTE	STARTG&177400/400,STARTG&377
	.BYTE	STARTG&177400/400,STARTG&377
	.END
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                  